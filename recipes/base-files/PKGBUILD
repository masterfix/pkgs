pkgname=base-files
pkgver=20210222
pkgrel=1
pkgdesc='Base Linux files'
arch=('any')
license=('GPL')
url='https://www.archlinux.org'
source=('profile')

package() {
  cd "$pkgdir"

  # setup root filesystem
  for d in boot dev etc home mnt usr var opt run; do
    install -d -m755 $d
  done
  install -d -m555 proc
  install -d -m555 sys
  install -d -m0750 root
  install -d -m1777 tmp

  # setup /etc
  install -d etc/{ld.so.conf.d,skel,profile.d}
  for f in profile; do
    install -m644 "$srcdir"/$f etc/
  done
  ln -s ../proc/self/mounts etc/mtab

  # setup /usr hierarchy
  for d in bin include lib share/{misc,pixmaps} src; do
    install -d -m755 usr/$d
  done
  for d in {1..8}; do
    install -d -m755 usr/share/man/man$d
  done

  # add bin symlinks
  ln -s usr/bin bin
  ln -s usr/bin sbin
  ln -s bin usr/sbin

  # setup /usr/local hierarchy
  for d in bin etc include lib man sbin share src; do
    install -d -m755 usr/local/$d
  done
  ln -s ../man usr/local/share/man
}

md5sums=('448fe7cbcb566e8d318dc0db0c9cb710')
