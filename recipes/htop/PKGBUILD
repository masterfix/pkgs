pkgname=htop
pkgver=3.0.2
pkgrel=1
pkgdesc='Interactive process viewer'
arch=('aarch64')
url='https://htop.dev/'
license=('GPL')
depends=('libnl')
options=('!emptydirs')
source=("https://github.com/htop-dev/htop/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('b4744a3bea279f2a3725ed8e5e35ffd9cb10d66673bf07c8fe21feb3c4661305')

prepare() {
  cd "$pkgname-$pkgver"

  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --enable-cgroup \
      --enable-delayacct \
      --enable-openvz \
      --enable-unicode \
      --enable-vserver

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
