pkgname=bash
_basever=5.1
_patchlevel=4
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc='The GNU Bourne Again shell'
arch=('aarch64')
license=(GPL)
url='https://www.gnu.org/software/bash/bash.html'
backup=(etc/bash/bashrc
	etc/bash/bash_logout
	etc/skel/.bashrc
	etc/skel/.bash_profile
	etc/skel/.bash_logout)
source=(https://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz
	bashrc
	bash_logout
        skel.bashrc
        skel.bash_profile
        skel.bash_logout)
if [[ $((10#${_patchlevel})) -gt 0 ]]; then
  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    source=(${source[@]} https://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//.}-$(printf "%03d" $_p))
  done
fi

prepare() {
  cd $pkgname-$_basever

  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    echo "applying patch bash${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../bash${_basever//.}-$(printf "%03d" $_p)
  done
}

build() {
  cd $pkgname-$_basever

  _bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/usr/local/sbin:/usr/local/bin:/usr/bin\"\'
               -DSTANDARD_UTILS_PATH=\'\"/usr/bin\"\'
               -DSYS_BASHRC=\'\"/etc/bash/bashrc\"\'
               -DSYS_BASH_LOGOUT=\'\"/etc/bash/bash_logout\"\'
               -DNON_INTERACTIVE_LOGIN_SHELLS)
  export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

  ./configure \
    --prefix=/usr \
    --with-curses \
    --enable-readline \
    --without-bash-malloc \
    --with-installed-readline
  make
}

package() {
  make -C $pkgname-$_basever DESTDIR="$pkgdir" install

  # /bin/sh symlink
  ln -s bash "$pkgdir/usr/bin/sh"

  # system-wide configuration files
  install -dm755 "$pkgdir/etc/bash/"
  install -Dm644 bashrc "$pkgdir/etc/bash/bashrc"
  install -Dm644 bash_logout "$pkgdir/etc/bash/bash_logout"

  # user configuration file skeletons
  install -dm755 "$pkgdir/etc/skel/"
  install -m644 skel.bashrc "$pkgdir/etc/skel/.bashrc"
  install -m644 skel.bash_profile "$pkgdir/etc/skel/.bash_profile"
  install -m644 skel.bash_logout "$pkgdir/etc/skel/.bash_logout"

  # remove bashbug tool
  rm $pkgdir/usr/bin/bashbug
}

md5sums=('bb91a17fd6c9032c26d0b2b78b50aff5'
         'd67ec198cbd0fd00af0b8d6e69cbfdc0'
         'd41d8cd98f00b204e9800998ecf8427e'
         'bbe73de1d0b990910a527d1c86ce7854'
         'd3523af1e066ad0ca01ae52ee5661411'
         'd41d8cd98f00b204e9800998ecf8427e'
         '57641ddbf92fca25df92a443e36f285a'
         'aed44842ed1a05fcfc3ef146991fdaef'
         'bf96455600a86420d69f5166575192dd'
         'd2c524dba0eea5dc5f00849cc84376a0')
